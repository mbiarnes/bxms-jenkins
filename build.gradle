
// reference from job-dsl-gradle example https://github.com/sheehan/job-dsl-gradle-example
// use ./gradlew test to test all dsl .groovy files locally

apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'  // folder to store dsl .groovy files
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    // add in a local repository to store third party plugins if there is any
    // Note that flat dir does not support maven pom files
    //flatDir(dirs: 'lib')
    maven {
        url 'https://repo.jenkins-ci.org/public/'
        flatDir(dirs: 'lib')
    }

}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        //exclude group: 'xerces'
    }
}

dependencies {
    compile "ca.szc.configparser:java-configparser:0.1"
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"
    compile 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.4'
    compile 'commons-lang:commons-lang:2.6'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.2@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:6.1.2@jar'

    // plugins to install in test instance
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.1.2'
    testPlugins 'org.jenkins-ci.plugins:jms-messaging:1.0.30@hpi'
  	testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.4'
    testPlugins 'com.redhat.jenkins.plugins:redhat-ci-plugin:1.5.0@hpi'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    //format the file to name.extension
    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }


    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
